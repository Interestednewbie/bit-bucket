###
### Commands for manipulating arbitrary windows: changing window state
###
### These commands need to be application-specific in some cases to
### override broken DNS built-ins.
###


  # use extension for non-elevated applications for improved speed,
  # less flicker:
Maximize() := Window.Maximize()
              If(Window.Success(), "", 
  # worse case: lower corner->upper corner->half->maximized
                SendSystemKeys({win+up}{win}{esc}) 
                If(Window.Maximized(), "",
                  SendSystemKeys({win+up}{win}{esc}) 
                  If(Window.Maximized(), "",
                    SendSystemKeys({win+up}{win}{esc})
                  )));

  # use extension for non-elevated applications for improved speed:
Minimize() := Window.Minimize()
	      If(Window.Success(), "", 
	         If(Window.Maximized(), SendSystemKeys({win+down_2}),
		 			SendSystemKeys({win+down})));

  # use extension for non-elevated applications to avoid annoying
  # extra maximization, flickering:
Restore()  := Window.Restore()
	      If(Window.Success(), "", 
	        Maximize() SendSystemKeys({win+down}));

  # extension seems to be more reliable for some programs:
Close()    := Window.Close()
	      If(Window.Success(), "", SendSystemKeys({alt+f4}));


#
# These replace DNS built-ins of the same name.
#
# I had to disable Freesr's Restore Window as well.
#

(minimize | minimise) [the] window = Minimize();
maximize	      [the] window = Maximize();
restore		      [the] window = Restore();
close		      [the] window = Close();
