### 
### Functions for (incrementally) copying files using Rsync
### 
### Additional faux Rsync options:
### 
###   -force:       copy a file even if destination file has same time and
###                 size
###
###   -replace-dir: delete destination files that no longer exist in the source;
###                 must copy a directory not a pattern (e.g., *) for
###                 this to work.
###
###   -type:        just type the Rsync command rather than executing it
###                 immediately.
###
### Default options automatically exclude .svn
###

include "extended_string.vch";
include "control.vch";
include "file_access.vch";


## 
## Special pathname conventions are used with Rsync:
## 
## Form is one of:
##   @[{work,foil}:]<Unix pathname> 
##   @|user@host:<Unix pathname> 
##   an absolute C-drive <PC pathname> after PCfromPC(-) abbreviations
## 

_Unix2Rsync(path) :=
    Replace4(@@$path, 
        "@@work:",   PWork()	     ":",
        "@@foil:",   PHomeFromPC()   ":",
        "@@|",       "",
	"@@",        PrimaryFromPC() ":");

  # here PCfromPC($path) must be an absolute path on drive C:
_PC2Rsync(path) :=
    '/cygdrive/c/' Replace3(PCfromPC($path), "\", "/", "C:/", "", "c:/", "");

_Either2Rsync(path) :=
    If(EQ(Left($path,1), "@"),
       _Unix2Rsync(Mid($path,1)),
       _PC2Rsync($path));

_Path(path) := '"' _Either2Rsync($path) '"';


## 
## Generating the Rsync command itself:
## 

# note: use -e 'ssh -v -v -v' arguments to rsync to debug it

_Options(options) :=
      # default options:
      #   preserve times, show progress, use compression, preserve permissions,
      #   copy recursively, exclude .svn subdirectories
    '-t --progress -z -p -r --exclude .svn '
    Replace3($options, -type,"", -force,--ignore-times, -replace-dir,--delete);

TypeRsync(options, source, destination) :=
      # Subprocess.System and ShellExecute can't handle spaces/quotes
      # in the executable name:
    #'"' PCfromPC(Cygwin() /rsync.exe) '" -e ' Cygwin() '\ssh.exe '
    PCfromPC(Cygwin() /rsync.exe) ' -e ' Cygwin() '\ssh.exe '
    _Options($options) ' ' _Path($source) ' ' _Path($destination);

  # this fires off a background task to do the work:
Rsync(options, source, destination) :=
    If(EQ(Left($options,5), "-type"),
       TypeRsync(Mid($options,5), $source, $destination) " && ",
       ShellExecute(TypeRsync($options, $source, $destination)));

  # this waits for the copy to complete before returning; runtime error if it fails
SyncRsync(options, source, destination) :=
    If(EQ(Left($options,5), "-type"),
       TypeRsync(Mid($options,5), $source, $destination) " && ",
         # Subprocess.Run doesn't handle quoted arguments properly:
       Subprocess.System(TypeRsync($options, $source, $destination)));


##
## Making a local (current PC) temporary copy of a computer UNIX file
## or directory; the file(s) last until the next reboot
##

Filename(source)  := Split($source, "/", -1);
Directory(source) := Eval("$source[0:$source.rindex('\\')]");


  # the PC location of the copy made by the last MakeLocal call
Local() := Variable.Get(import:path);

  # call this to make a temporary copy of $source on PC
MakeLocal(source) :=
    Variable.Set(import:path, PCfromPC(~/scratch/imports/ 
                                         Experimental.Hash(Date.Now("%c"))
				         "/" Filename($source)
				       ))
    Subprocess.System("mkdir " Directory(Local()))
    SyncRsync("", $source, Directory(Local()) "/");


##
## Uploading output from a PC application to a computer UNIX directory
##

UploadDir() := Variable.Get(import:upload);

PrepareUpload(target) :=
    Variable.Set(import:target, $target)
    Variable.Set(import:upload, PCfromPC(~/scratch/imports/ 
                                         Experimental.Hash(Date.Now("%c"))
				       ))
    Subprocess.System("mkdir " UploadDir()) 
    Wait(100);  # need to wait for System window to go away

DoUpload() := Rsync("-force", UploadDir()/, Variable.Get(import:target))
    Variable.Unset(import:target);
